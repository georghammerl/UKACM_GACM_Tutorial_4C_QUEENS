reset
reset aprepro
undo on

# ===============
# ||  VARIABLES   ||
# ===============
# { length = 5.0 }
# { thickness = 1.0 }
# { elsize_fine = 0.4 }  # for higher accuracy, choose 0.4
# { elsize_coarse = 1.25 }
# { axial_intervals_thicker_block = 4 }
# { axial_intervals_chamfer = 3 }
# { total_height = length*6.5 }
# { head_width = length*(1+(1-cosd(30))) }
# { coarseness = 5 }   must be between 1 and 9, default 5 is appropriate, 6 is faster, but still okay.

# finest mesh for tutorial with approx 28k dofs: elsize_fine = 0.4, elsize_coarse = 1.25, axial_intervals_thicker_block = 4, axial_intervals_chamfer = 3, coarseness = 5
# intermediate mesh for tutorial with approx 11k dofs: elsize_fine = 0.4, elsize_coarse = 1.25, axial_intervals_thicker_block = 4, axial_intervals_chamfer = 3, coarseness = 6
# coarsest mesh for tutorial with approx 5k dofs: elsize_fine = 0.8, elsize_coarse = 2.5, axial_intervals_thicker_block = 3, axial_intervals_chamfer = 3, coarseness = 7

# ================
# ||  GEOMETRY  ||
# ================
# 2D shape
create surface rectangle width {length}  height {5*length} zplane
move surface 1 x {length/2.0} y {length*2.5} include_merged
create curve arc radius {length} center location {2*length} {5*length} 0 normal 0 0 1 start angle 150 stop angle 180
create curve location 0 {5*length} 0 location 0 {5*head_width} 0
create curve vertex 5 8
create surface curve 1 5 7 6
create curve location 0 {total_height} 0 location {head_width} {total_height} 0
create surface vertex 8 5 12 11
imprint tolerant surface 1 3 2
merge surface all

# create 3D geometry
sweep surface all perpendicular distance { thickness }
merge volume all

# ===============
# ||  MESHING  ||
# ===============

curve 2 scheme bias fine size {elsize_fine} coarse size {elsize_coarse} start vertex 3
curve 4 scheme bias fine size {elsize_fine} coarse size {elsize_coarse} start vertex 4
curve 14 scheme bias fine size {elsize_fine} coarse size {elsize_coarse} start vertex 18
curve 19 scheme bias fine size {elsize_fine} coarse size {elsize_coarse} start vertex 19
curve 5 6 25 29 interval {axial_intervals_chamfer}
curve 5 6 25 29 scheme equal
curve 11 13 30 35 interval {axial_intervals_thicker_block}
curve 11 13 30 35 scheme equal
volume all size auto factor {coarseness}
mesh volume all

# =================
# ||  NODE SETS  ||
# =================
nodeset 1 add surface 5
nodeset 1 name "YSYMM"
nodeset 2 add surface 1 2 3 8 13 18
nodeset 2 name "ZSYMM"
nodeset 3 add surface 6 10 14
nodeset 3 name "XSYMM"
nodeset 4 add surface 15
nodeset 4 name "TOP"

# ========================
# ||  BLOCK DEFINITION  ||
# ========================
set duplicate block elements off
block 1 add volume all
block 1 element type hex27

# ====================
# ||  WRITE EXODUS  ||
# ====================
export mesh "tutorial_solid_geo.e"  overwrite
